# configure.in for CCured           -*- sh -*-
# Process this file with autoconf to produce a configure script.

# Autoconf runs this through the M4 macroprocessor first; lines
# starting with "dnl" are comments to M4.  The result is a bash
# script; any text which isn't an M4/autoconf directive gets
# copied verbatim to that script.

# sm: changed this file to use '#' for comments, since that's
# just as good (since this becomes an 'sh' script)

# -------------- usual initial stuff -------------
# this simply names a file somewhere in the source tree to verify
# we're in the right directory
AC_INIT(src/cil.mli)

# make sure I haven't forgotten to run autoconf
if test configure -ot configure.in; then
  AC_MSG_ERROR(configure is older than configure.in; you forgot to run autoconf)
fi

# check for C compiler; this typically finds gcc
AC_PROG_CC

# find system type (using this macro means we must include
# the files install-sh, config.sub, and config.guess (all from
# the autoconf distribution) in our source tree!)
AC_CANONICAL_SYSTEM


# # --------- example of a specialized test ----------
# # determine endianness
# if test "$ENDIANNESS" != "-DLITTLE_ENDIAN" -a \
#         "$ENDIANNESS" != "-DBIG_ENDIAN" ; then
#   AC_MSG_CHECKING(endianness)
#   AC_TRY_RUN(
#     [
#       /* x must be global to work around an egcs-1.1.2 optimizer bug */
#       int x = 0;     /* start with all bits 0 */
#       int main()
#       {
#         /* little endian: sets x to 1 */
#         /* big endian: sets x to a large number (e.g. 0x01000000 if 32 bits) */
#         *((char*)(&x)) = 1;
#
#         if (x == 1) {
#           exit(0);     /* success will mean little endian */
#         }
#         else {
#           exit(1);     /* big endian */
#         }
#       }
#     ],
#
#     AC_MSG_RESULT(little endian)
#     ENDIANNESS=-DLITTLE_ENDIAN,
#
#     AC_MSG_RESULT(big endian)
#     ENDIANNESS=-DBIG_ENDIAN,
#
#     AC_MSG_ERROR([
#       To cross-compile you must set the environment variable
#       ENDIANNESS to either -DLITTLE_ENDIAN or -DBIG_ENDIAN
#     ])
#   )
# fi


# ---------------- generic functions -----------------
# debugging diagnostic; set to 'echo' to debug or 'true' for production
diagnostic() {
  #echo "$@"
  true "$@"
}

# determine if a binary is in the path
binaryExists() {
  # on cygwin, 'which' always returns success, so use 'type' instead
  if type "$1" >/dev/null 2>&1; then
    return 0
  else
    return 1
  fi
}


# -------------- portable configuration ----------------
# this specifies the root of the source tree; it's just the
# directory where ./configure runs, except on cygwin, which
# overrides this below
CCUREDHOME=`pwd`

# is the microsoft compiler available?
AC_MSG_CHECKING(for msvc cl.exe (optional))
if binaryExists cl; then
  AC_MSG_RESULT(found)
  HAS_MSVC=yes
else
  AC_MSG_RESULT(not found)
  HAS_MSVC=no
fi

# is ocaml available?
# needed binaries: ocamllex ocamlyacc ocamldep ocamlopt ocamlc
ocamlDownloadInstructions="
      OCaml can be downloaded from http://caml.inria.fr/ocaml/.
      After downloading and unpacking the source distribution, in the ocaml
      directory, do
        ./configure
        make world
        make opt
        make install
      Then come back here and re-run ./configure."

# required major/minor
reqMaj=3
reqMin=04
AC_MSG_CHECKING(ocaml version is at least $reqMaj.$reqMin)
if binaryExists ocamlc; then
  # what version?
  ver=`ocamlc -v | grep version | sed 's/^.*version //'`
  diagnostic "ver is $ver"
  # major: anything before the .
  major=`echo $ver | sed 's/\..*$//'`
  diagnostic "major is $major"
  # minor: numbers after the .
  # (the outer level of bracket-quotation protects the inner brackets)
  [minor=`echo $ver | sed 's/^.*\.\([0-9]\+\).*$/\1/'`]
  diagnostic "minor is $minor"

  # I would think autoconf would already have a facility for doing
  # these kinds of major/minor version checks, but I can't find it
  if test "$major" -gt $reqMaj -o \
          "$major" -ge $reqMaj -a "$minor" -ge $reqMin; then
    AC_MSG_RESULT([version is $ver, ok])
  else
    AC_MSG_ERROR([
      I found OCaml version $ver; CCured requires at least $reqMaj.$reqMin.
      Please download a newer OCaml distribution.
      $ocamlDownloadInstructions
    ])
  fi

  # check for existence of other binaries
  AC_MSG_CHECKING(existence of related ocaml tools)
  if binaryExists ocamllex && \
     binaryExists ocamlyacc && \
     binaryExists ocamldep && \
     binaryExists ocamlopt; then
    AC_MSG_RESULT(ok)
  else
    AC_MSG_ERROR([
      At least one of ocamllex, ocamlyacc, ocamldep or ocamlopt is missing.
      In particular, ocamlopt requires you to "make opt" when building
      OCaml from source.  Please make sure all these tools are built and
      in the path.
    ])
  fi
else
  AC_MSG_ERROR([
      The "ocamlc" OCaml compiler was not found in the path.  Most of
      CCured is written in the OCaml language, so its compiler is required.
      $ocamlDownloadInstructions
  ])
fi


# additional tools we might check for:
#   - gnu make
#   - perl


# ----------- platform-specific code -------------
# $target is typically processor-vendor-os
case "$target" in
  # linux
  *86*linux*)
    AC_MSG_RESULT(configuring for linux/x86)

    ARCHOS=x86_LINUX
    ;;

  # cygwin
  *86*cygwin*)
    AC_MSG_RESULT(configuring for Cygwin on win32/x86)

    ARCHOS=x86_WIN32

    # override CCUREDHOME; even on cygwin we want forward slashes
    # sm: I moved folded this into what I hope will be the only
    # case-analysis of machine type
    CCUREDHOME_cygwin=`pwd`
    CCUREDHOME=`cygpath -wa $CCUREDHOME_cygwin  | sed -e "s/\\\\\/\\//g"`
    ;;

  *)
    AC_MSG_ERROR([
      Unsupported platform $target -- sorry.
      Linux/x86 and Win32/Cygwin/x86 are the supported platforms.
    ])
    ;;
esac
CILHOME=$CCUREDHOME
do_not_edit="Do not edit this file. It was generated automatically from"

# ----------- some stuff 'autoscan' put here --------------
# (autoscan is part of the autoconf distribution)

# checks for header files
AC_HEADER_STDC
AC_CHECK_HEADERS(strings.h sys/time.h unistd.h)

# checks for typedefs, structures, and compiler characteristics
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME

# checks for library functions; more autoscan stuff
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(mkdir select socket)


# ----------------- finish up -------------------
# names of the variables that get substituted in files; for example,
# write @ARCHOS@ somewhere in a written file to get it substituted
AC_SUBST(ARCHOS)
AC_SUBST(CCUREDHOME)
AC_SUBST(CILHOME)
AC_SUBST(do_not_edit)
AC_SUBST(HAS_MSVC)

# finish the configure script and generate various files; ./configure
# will apply variable substitutions to <filename>.in to generate <filename>;
# I find it useful to mark generated files as read-only so I don't
# accidentally edit them (and them lose my changes when ./configure
# runs again); I had originally done the chmod after AC_OUTPUT, but
# the problem is then the chmod doesn't run inside ./config.status

# MY_AC_CONFIG_FILES(filename)
# do AC_CONFIG_FILES(filename, chmod a-w filename)
define([MY_AC_CONFIG_FILES],
[{
  if test -f [$1].in; then
    AC_CONFIG_FILES([$1], chmod a-w [$1])
  else
    true
    #echo "skipping [$1] because it's not in this distribution"
  fi
}])

MY_AC_CONFIG_FILES(Makefile)
MY_AC_CONFIG_FILES(test/Makefile)
MY_AC_CONFIG_FILES(Makefile.cil)
MY_AC_CONFIG_FILES(lib/cilly.pl)
MY_AC_CONFIG_FILES(lib/ccured.pl)
MY_AC_CONFIG_FILES(lib/scaninfer)
MY_AC_CONFIG_FILES(lib/scaninfer.bat)

AC_OUTPUT()

# show the user what the variables have been set to
cat <<EOF

CCured/CIL configuration:
  architecture/OS:            ARCHOS       $ARCHOS
  source tree root:           CCUREDHOME   $CCUREDHOME
  (optional) cl.exe found:    HAS_MSVC     $HAS_MSVC
EOF
